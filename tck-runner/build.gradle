plugins {
    id "org.eclipse.odi.build.internal.base"
    id "com.adarshr.test-logger"
}

description = 'CDI TCK runner'

dependencies {
    annotationProcessor projects.odiProcessorCdi

    implementation projects.odiCdi
    implementation projects.odiProcessorCdi
    implementation libs.cdi.api
    implementation (libs.jul.to.slf4j) {
        version {
            require mn.versions.slf4j.get()
        }
    }
    implementation libs.cdi.tck.api
    implementation libs.cdi.tck.impl
    implementation mn.logback
    implementation mn.micronaut.inject.java

    testImplementation(libs.cdi.tck.impl) {
        artifact {
            classifier = "sources"
        }
    }
    testCompileOnly(libs.cdi.tck.impl) {
        artifact {
            classifier = "suite"
            type = "xml"
        }
    }
}

testlogger {
    showExceptions false
    showStackTraces false
    showFullStackTraces false
    showStandardStreams false
    showPassedStandardStreams false
    showSkippedStandardStreams false
    showFailedStandardStreams false
}

tasks.register('fullTckTest', Test) {
    scanForTestClasses false
    useTestNG {
        doFirst {
            def testSuiteLocation = project.configurations.testCompileClasspath.filter {
                it.name.contains("cdi-tck-core-impl") && it.name.contains("xml")
            }.asPath
            suites new File(testSuiteLocation)
        }
        excludeGroups("cdi-full", "javaee-full")
        systemProperties = [
                'org.jboss.cdi.tck.libraryDirectory': "${buildDir}/tck-lib",
        ]
    }
}

tasks.register('singleTest', Test) {
    scanForTestClasses false
    useTestNG {
        suiteXmlBuilder().suite(name: 'Sample Suite') {
            test(name : 'Sample Test') {
                classes('') {
                    'class'(name: 'org.jboss.cdi.tck.tests.build.compatible.extensions.syntheticObserverOfParameterizedType.SyntheticObserverOfParameterizedTypeTest') {
                    }
                }
            }
        }
        excludeGroups("cdi-full", "javaee-full")
        systemProperties = [
                'org.jboss.cdi.tck.libraryDirectory': "${buildDir}/tck-lib",
        ]
    }
}

test {
    scanForTestClasses false
    useTestNG {
        suites file("passingTests.xml")
        excludeGroups("cdi-full", "javaee-full")
        systemProperties = [
                'org.jboss.cdi.tck.libraryDirectory': "${buildDir}/tck-lib",
        ]
    }
}