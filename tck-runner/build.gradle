description = 'CDI TCK runner'

dependencies {
    annotationProcessor 'io.micronaut:micronaut-inject-java'
    annotationProcessor 'io.micronaut:micronaut-validation'
    annotationProcessor project(':odi-processor-cdi')

    implementation "jakarta.enterprise:jakarta.enterprise.cdi-api:$cdiApiVersion"
    implementation project(':odi-cdi')
    implementation project(':odi-processor-cdi')
    implementation 'org.slf4j:jul-to-slf4j:1.7.30'
    implementation 'ch.qos.logback:logback-classic'
    implementation 'io.micronaut:micronaut-inject-java'
    implementation "jakarta.enterprise:cdi-tck-api:$cdiTckApi"
    implementation "jakarta.enterprise:cdi-tck-core-impl:$cdiTckApi"

    testImplementation "jakarta.enterprise:cdi-tck-core-impl:$cdiTckApi:sources"
    testCompileOnly "jakarta.enterprise:cdi-tck-core-impl:$cdiTckApi:suite@xml"
    testImplementation 'commons-lang:commons-lang:2.6'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'javax.servlet:servlet-api:2.5'
    testImplementation 'org.apache.ant:ant:1.10.11'
    testImplementation "org.testng:testng:7.4.0"
}

testlogger {
    showExceptions false
    showStackTraces false
    showFullStackTraces false
    showStandardStreams false
    showPassedStandardStreams false
    showSkippedStandardStreams false
    showFailedStandardStreams false
}

tasks.register('fullTckTest', Test) {
    scanForTestClasses false
    useTestNG {
        doFirst {
            def testSuiteLocation = project.configurations.testCompileClasspath.filter {
                it.name.contains("cdi-tck-core-impl") && it.name.contains("xml")
            }.asPath
            suites new File(testSuiteLocation)
        }
        excludeGroups("cdi-full", "javaee-full")
        systemProperties = [
                'org.jboss.cdi.tck.libraryDirectory': "${buildDir}/tck-lib",
        ]
    }
}

test {
    scanForTestClasses false
    useTestNG {
        suites file("passingTests.xml")
        excludeGroups("cdi-full", "javaee-full")
        systemProperties = [
                'org.jboss.cdi.tck.libraryDirectory': "${buildDir}/tck-lib",
        ]
    }
}